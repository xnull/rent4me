allprojects {
    repositories {
        mavenCentral()
    }

    ext {
        springVersion = '4.0.5.RELEASE'
        hibernateVersion = '4.2.2.Final'
        jolboxDSPoolVersion = '0.8.0.RELEASE'
        springSecurityVersion = '3.2.4.RELEASE'
        logbackVersion = '1.1.2'
    }
}

subprojects {
    afterEvaluate {
        sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
        sourceSets.test.output.resourcesDir = sourceSets.test.output.classesDir

        plugins.withType(JavaPlugin) {
            def javaVersion = '1.7'

            sourceCompatibility = javaVersion
            targetCompatibility = javaVersion

            version = '1.0'

            idea {
                project {
                    jdkName = javaVersion
                    languageLevel = javaVersion
                }
            }

            dependencies {
                //spring
                compile "org.springframework:spring-context:${springVersion}"
                compile "org.springframework:spring-context-support:${springVersion}"
                compile "org.springframework.security:spring-security-core:${springSecurityVersion}"

                //logger
                compile 'org.slf4j:slf4j-api:1.7.7'
                compile "ch.qos.logback:logback-classic:${logbackVersion}"
                compile "ch.qos.logback:logback-core:${logbackVersion}"

                //test
                testCompile 'junit:junit:4.11'
                testCompile "org.hamcrest:hamcrest-all:1.3"
                testCompile 'org.mockito:mockito-all:1.9.5'
                testCompile "org.dbunit:dbunit:2.4.9"
                testCompile "org.springframework:spring-test:${springVersion}"
                testCompile "com.jolbox:bonecp:${jolboxDSPoolVersion}"
            }
        }
    }
}