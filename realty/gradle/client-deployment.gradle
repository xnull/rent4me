configurations {
    sshAntTask
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.9.2'
}

ant.taskdef(
        name: 'scp',
        classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
        classpath: configurations.sshAntTask.asPath)

ant.taskdef(
        name: 'ssh',
        classname: 'org.apache.tools.ant.taskdefs.optional.ssh.SSHExec',
        classpath: configurations.sshAntTask.asPath)

task clientDeployment {
    group = 'Infrastructure'

    doLast {
        // Create a new file for each execution to make
        // sure that execution doesn't fails in case
        // idententy of host has been changed.
        def knownHosts = File.createTempFile("knownhosts", "txt")
        def user = 'root'
        def host = 'rent4.me'
        def privateKey = file('keys/myKey')

        try {
            // Example to copy files to a remote host.
            ant.scp(
                    file: file("build.gradle"),
                    todir: "${user}@${host}:~",
                    keyfile: privateKey,
                    trust: true,
                    knownhosts: knownHosts
            )

            // Example to execute a command on the remote host.
            /*ant.ssh(
                    host: host,
                    username: user,
                    keyfile: privateKey,
                    trust: true,
                    knownhosts: knownHosts,
                    command: "ls /"

            )*/

        } finally {
            knownHosts.delete()
        }
    }
}